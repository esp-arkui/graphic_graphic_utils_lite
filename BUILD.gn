# Copyright (c) 2020-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#     http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/lite/config/component/lite_component.gni")
import("//build/lite/config/subsystem/graphic/config.gni")
import("//build/lite/ndk/ndk.gni")

lite_component("lite_graphic_utils") {
  features = [ ":graphic_utils" ]
  public_deps = features
}

ndk_lib("lite_graphic_utils_ndk") {
  lib_extension = ".so"
  deps = [ ":graphic_utils" ]
  head_files = [ "interfaces/kits" ]
}

lite_component("lite_graphic_hals") {
  features = [ ":graphic_hals" ]
  public_deps = features
}

ndk_lib("lite_graphic_hals_ndk") {
  lib_extension = ".so"
  deps = [ ":graphic_hals" ]
  head_files = []
}

shared_library("graphic_utils") {
  if (enable_graphic_dualcore == true) {
    defines = [ "HAL_CPU_NUM = 2", ]
  }

  sources = [
    "frameworks/color.cpp",
    "frameworks/geometry2d.cpp",
    "frameworks/graphic_math.cpp",
    "frameworks/graphic_timer.cpp",
    "frameworks/graphic_performance.cpp",
    "frameworks/hal_cpu.cpp",
    "frameworks/hal_tick.cpp",
    "frameworks/mem_api.cpp",
    "frameworks/pixel_format_utils.cpp",
    "frameworks/queue.c",
    "frameworks/style.cpp",
    "frameworks/transform.cpp",
    "frameworks/version.cpp",
    "frameworks/graphics/vertexprimitive/graphic_geometry_arc.cpp",
    "frameworks/graphics/vertexprimitive/graphic_geometry_bezier_arc.cpp",
    "frameworks/graphics/vertexprimitive/graphic_geometry_curves.cpp",
    "frameworks/graphics/vertexprimitive/graphic_geometry_rounded_rect.cpp",
    "frameworks/graphics/transform/graphic_transform_trans_affine.cpp",
    "frameworks/graphics/vertexgenerate/graphic_vertex_generate_dash.cpp",
    "frameworks/graphics/vertexgenerate/graphic_vertex_generate_stroke.cpp",
  ]
  include_dirs = [ "frameworks","interfaces/kits" ]
  public_configs = [ ":graphic_utils_public_config" ]
  deps = [ "//third_party/bounds_checking_function:libsec_shared" ]
  public_deps =
      [ "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared" ]
  cflags = [ "-fPIC" ]
  cflags += [ "-Wall" ]
  cflags_cc = cflags
}

config("graphic_utils_public_config") {
  include_dirs = [
    "interfaces/innerkits",
    "interfaces/kits",
  ]
  if (ohos_kernel_type == "liteos_a") {
    defines = [ 
      "GRAPHIC_GEOMETYR_ENABLE_LINECAP_STYLES_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_LINEJOIN_STYLES_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_ELLIPSE_CIRCLE_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_BEZIER_ARC_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_ARC_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_ROUNDEDRECT_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_DASH_GENERATE_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_BLUR_EFFECT_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_SHADOW_EFFECT_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_GRADIENT_FILLSTROKECOLOR=1",
      "GRAPHIC_GEOMETYR_ENABLE_PATTERN_FILLSTROKECOLOR=1",
      "GRAPHIC_GEOMETYR_ENABLE_HAMONY_DRAWIMAGE=1",
      "GRAPHIC_GEOMETYR_ENABLE_HAMONY_DRAWTEXT=1",
    ]
  }
  if (ohos_kernel_type == "liteos_m") {
    defines = [ 
      "GRAPHIC_GEOMETYR_ENABLE_LINECAP_STYLES_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_LINEJOIN_STYLES_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_ELLIPSE_CIRCLE_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_BEZIER_ARC_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_ARC_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_ROUNDEDRECT_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_DASH_GENERATE_VERTEX_SOURCE=1",
      "GRAPHIC_GEOMETYR_ENABLE_BLUR_EFFECT_VERTEX_SOURCE=0",
      "GRAPHIC_GEOMETYR_ENABLE_SHADOW_EFFECT_VERTEX_SOURCE=0",
      "GRAPHIC_GEOMETYR_ENABLE_GRADIENT_FILLSTROKECOLOR=0",
      "GRAPHIC_GEOMETYR_ENABLE_PATTERN_FILLSTROKECOLOR=0",
      "GRAPHIC_GEOMETYR_ENABLE_HAMONY_DRAWIMAGE=0",
      "GRAPHIC_GEOMETYR_ENABLE_HAMONY_DRAWTEXT=1",
    ]
  }
}

shared_library("graphic_hals") {
  sources = [
    "frameworks/hals/gfx_engines.cpp",
    "frameworks/hals/hi_fbdev.cpp",
  ]
  include_dirs = [ "//foundation/graphic/wms/interfaces/innerkits" ]
  deps = [
    ":lite_graphic_utils",
    "//drivers/peripheral/display/hal:hdi_display",
  ]
  public_configs = [ ":graphic_hals_public_config" ]
  ldflags = [
    "-ldisplay_gfx",
    "-ldisplay_gralloc",
    "-ldisplay_layer",
  ]
}

config("graphic_hals_public_config") {
  include_dirs = [
    "interfaces/innerkits",
    "//drivers/peripheral/base",
    "//drivers/peripheral/display/interfaces/include",
  ]
}
